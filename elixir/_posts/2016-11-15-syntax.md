---
layout: post
title: Syntax
description: A quick reference for the basics of the Elixir language
tags: basics
date: 2016-11-15 16:05:00 -0300
image: assets/images/posts/potions-book.jpg
comments: true
---
## Contents
* [Variables](#variables)
* Basic types: [Integer](#integer), [Float](#float), [Atom](#atom), [Range](#range), [Regex](#regex)
* Collections: [Tuple](#tuple), [List](#list), [Keyword lists](#keyword-lists), [Map](#map), [Binary](#binary)
* [Structs](#structs), [Sigils](#sigils)
* [Operators](#operators)
* [Control flow](#control-flow), [Comprehensions](#comprehensions)
* [Functions](#functions), [Guard clauses](#guard-clauses)
* [Modules](#modules)
* [Protocols](#protocols)
* [Behaviours](#behaviours)
* [Errors](#errors)

## Variables

Elixir is a dynamic language and the type of a variable is determined by the value it holds. Pattern matching is done using the **match operator** `=`. You can use it to assign values to variables. Variables can be bound only once per match but can be reassigned in subsequent matches. Starting a variable with the **pin operator** `^` will force the variable to its existing value.

Variables starting with `_` are marked as unused variables and not using them will not raise any warning by the compiler. A single `_` will ignore values in pattern matching.

Variable names and functions have the following syntax: A lowercase ASCII letter or an underscore, followed by any number of lowercase or uppercase ASCII letters, numbers, or underscores. Optionally they can end in either an exclamation mark or a question mark. See the reference for [naming conventions](https://hexdocs.pm/elixir/naming-conventions.html).

---

## Types

### Integer

Integers can be decimal `1234`, hexadecimal `0xcafe`, octal `0o765` and binary `0b1010`. Commas are not allowed in numbers, but underscores are. So if you feel the need to mark your thousands so the numbers are more readable, use an underscore. You can write `12_000_000_000`.

### Float

Floats consist of numbers with a decimal place or scientific notation. `3.14`, `-808.08` and `12.043e-04` are examples. Maximum value of around `1.0e308`.

### Atom

Atoms are words that look just like variables, starting with a colon, like `:true` or even `:"chunky bacon"`. An atom’s name is its value and two atoms with the same name will always compare as being equal. In fact, the booleans `true` and `false` (and `nil`!) are in fact atoms:

```elixir
false == :false #=> true
```

[Erlang modules](http://elixir-lang.org/getting-started/erlang-libraries.html) are accessible using atoms, like `:io.format`. All module names are in fact atoms. Elixir modules like `Enum` are really `:Elixir.Enum`. Module names start with a capital letter and are [aliased](#alias) like that.

Atoms are never garbage collected and should never be converted from user input. The Erlang VM has a limit for the number of atoms that can be created.

### Range

A range is two integers separated by two dots like so: `1..5`. Can be increasing or decreasing and are represented internally as a [struct](#structs) with fields `:first` and `:last`.

### Regex

A regular expression can be created with `~r{regex}opts` and are represented internally as a [struct](#struct).

[Reference for Regex](https://hexdocs.pm/elixir/Regex.html) and [Elixir regular expression editor & tester](http://www.elixre.uk/).

## Collections

Collection functions use this rule to define its complexity:

* Size – constant time (pre-calculated)
* Length – linear (slower as input grows)

### Tuple

Tuples is an ordered collection of values, defined by curly brackets.

```elixir
{:ok, "hello"}
```

Tuples are stored contiguously in memory: getting the tuple size or accessing an element is fast, however adding or updating elements is expensive – because of immutability, the whole tuple needs to be copied in memory.

```elixir
# Kernel functions for working with tuples
elem/2, put_elem/3, tuple_size/1
```

**Records** are tuples where the first element is an atom, they are related to Erlang records.

### List

Lists are collection of values, defined by square brackets. The **cons operator** can be used to construct lists, separating its head from its tail.

```elixir
[1, 2, :hello]
# is the same as:
[ 1 | [ 2 | [ :hello | [] ] ]]
```

Lists are stored in memory as linked lists: accessing the length of a list is a linear operation (traversing the whole list is necessary). Updating a list is fast as long as you are prepending elements.

```elixir
# Kernel functions for working with lists
++/2, --/2, length/1, hd/1, tl/1
```

#### Keyword lists

A keyword list is a list of two element tuples. It is often used for options as the last argument of a function, where you can drop all of the braces.

```elixir
[{:one, 1}, {:two, 2}, {:three, 3}]
# can also be written as:
[one: 1, two: 2, three: 3]
```

* More than one of the same key
* Guarantees elements are ordered
* Keys must be atoms

### Map

Maps is a key-value store, created using the `%{}` syntax. Map keys don't follow any ordering.

```elixir
map = %{:a => 1, 5 => "work"} #=> %{5 => "work", :a => 1}
map[:a] #=> 1

user = %{name: "John", age: 27}

# Strict access
user.age #=> 27
user.address
# ** (KeyError) key :address not found in: %{age: 27, name: "John"}

# Dynamic access
user[:age] #=> 27
user[:address] #=> nil

# updating values
older_john = %{user | age: 28} #=> %{age: 28, name: "John"}
```

* Only one of the same key
* Keys can be of any type
* Useful when pattern matching and for many items

```elixir
# Kernel functions and macros for working with nested maps
get_and_update_in/2, get_in/2, map_size/1 put_in/2, update_in/2
```

#### Structs

Structs are modules that define custom maps with specific keys and default values, and only those keys are allowed. Structs are maps with a `__struct__` key holding the name of its module. They don't implement the Access or Enumerable protocol.

```elixir
defmodule User do
  defstruct [name: "John", age: 27]
end

john = %User{} #=> %User{age: 27, name: "John"}
meg = %User{name: "Megan"} #=> %User{name: "Megan", age: 27}

inspect(john, structs: false)
#=> "%{__struct__: User, age: 27, name: \"John\"}"

# Strict access only
meg.name #=> "Megan"

john[:name]
# ** (UndefinedFunctionError) function User.fetch/2 is undefined
# (User does not implement the Access behaviour)
#              User.fetch(%User{age: 27, name: "John"}, :name)
```

Using `@enforce_keys` makes the keys mandatory when creating a new struct.

```elixir
defmodule User do
  @enforce_keys [:name]
  defstruct [name: "John", age: 27]
end

john = %User{}
# ** (ArgumentError) the following keys must also be given when
# building struct User: [:name]
#    expanding struct: User.__struct__/1
```

### Binary

Binaries are sequence of bytes enclosed by `<< >>` and separated with `,`.

```elixir
# Kernel functions for working with binaries
binary_part/3, bit_size/1, byte_size/1, is_binary/1, is_bitstring/1
```

#### Strings

Strings are binaries encoded in UTF-8 and are represented as `"chunky bacon"`. String interpolation is supported with `#{}`. A codepoint is a single Unicode character, represented by one or more bytes. A grapheme may consist of multiple codepoints but are perceived as a single character by the reader.

Escape sequences:

* `\"` – double quote
* `\'` – single quote
* `\\` – single backslash
* `\a` – bell/alert
* `\b` – backspace
* `\d` - delete
* `\e` - escape
* `\f` - form feed
* `\n` – newline
* `\r` – carriage return
* `\s` – space
* `\t` – tab
* `\v` – vertical tab
* `\0` - null byte
* `\xDD` - represents a single byte in hexadecimal (such as `\x13`)
* `\uDDDD` and `\u{D...}` - represents a Unicode codepoint in hexadecimal (such as `\u{1F600}`)

#### Sigils

Sigils are alternative syntax for working with textual representation.

```elixir
~s{hello dogs and cats} #=> "hello dogs and cats"
~w{dog cat}             #=> ["dog", "cat"]
```

| Default sigils ids  | Description                                          |
| :-----------------  | :--------------------------------------------------  |
| c                   | character list without escaping or interpolation     |
| C                   | character list with escaping and interpolation       |
| D                   | dates                                                |
| N                   | naive date times                                     |
| r                   | regular expression without escaping or interpolation |
| R                   | regular expression with escaping and interpolation   |
| s                   | string without escaping or interpolation             |
| S                   | string with escaping and interpolation               |
| T                   | times                                                |
| w                   | word lists without escaping or interpolation         |
| W                   | word lists with escaping and interpolation           |

It is possible to define you own sigil by creating a function `sigil_[id]`. Sigils are accessible in the module they are defined and other modules that import it.

```elixir
def sigil_i(string, []), do: String.to_integer(string)
```

---

## Operators

Operators are all defined by the [Kernel module](https://hexdocs.pm/elixir/Kernel.html). Reference for all operators can be found [here](https://hexdocs.pm/elixir/operators.html).

```elixir
=== !== == !=       # Strict and relaxed equality comparison
> >= < <=           # Comparison
and or not && || !  # Strict and relaxed boolean operators
+ - * / div rem     # Arithmetic operators
<> ++ -- ..         # Binary and list concatenation, diff, range
in                  # is the same as Enum.member?/2
```

Comparing different types follow this rule: `number < atom < reference < function < port < pid < tuple < map < list < binary`

The **pipe operator** `|>` gets the result of the left statement and passes it as the first argument to the function on the right.

```elixir
# without using the pipe operator
list = [1, 2, 3, 4, 5]
shuffled = Enum.shuffle(list)
IO.inspect(shuffled)
#=> [3, 4, 5, 1, 2]

# using the pipe operator
[1, 2, 3, 4, 5] |> Enum.shuffle |> IO.inspect
#=> [3, 4, 5, 1, 2]
```

Other operators include:

* `=` [match](#variables)
* `|` [cons](#list)
* `^` [pin](#variables)
* `&` [capture](#functions)
* `?` codepoint: returns the UTF-8 codepoint of the character following it.
* `=~` string match: returns `true` if the string on the left is a substring or a regular expression match of the string on the right.

---

## Control flow

### case

`case` is used to compare a value against many patterns. You can use [guard clauses](#guard-clauses) when pattern matching with `case`.

```elixir
case {1, 2, 3} do
  {4, 5, 6} ->
    "This clause won't match"
  {1, x, 3} ->
    "This clause will match and bind x to 2 in this clause"
  _ ->
    "This clause would match any value"
end
```

### if and unless

`if` and `unless` are useful to check for only one condition.

```elixir
if true do
  :ok
else
  :error
end
```

### cond

`cond` is useful to check multiple conditions. It is like `else if` in other languages.

```elixir
cond do
  2 + 2 == 5 ->
    "This will not be true"
  1 + 1 == 2 ->
    "But this will"
end
```

### with

`with` is used to combine matching clauses. If all clauses match, the `do` block is executed, otherwise the chain is aborted and the non-matched value is returned. [Guard clauses](#guard-clauses) can be used in patterns. An `else` option may be given that will run in case of a failed match. `with` is useful when there are nested `case` statements with error handling. Variables bound inside `with` are restricted to it.

```elixir
with {:ok, width} <- Map.fetch(opts, :width),
     {:ok, height} <- Map.fetch(opts, :height),
  do: {:ok, width * height}
#=> {:ok, 150} if both match
#=> :error if one does not
```

### Comprehensions

Comprehensions are used to loop over one or more collections, extracting values from each, filtering out some results and mapping them into another collection. A comprehension is made of three parts: generators, filters and collectables.

`for generator [, filter] [, into: value], do: expression`

Comprehensions discard all elements for which the filter expression returns false or nil. Variables bound inside `for` are restricted to it.

```elixir
multiple_of_3? = fn (n) -> rem(n, 3) == 0 end

for x <- 0..5, multiple_of_3?.(x) do: x*x
#=> [0, 9]

for {key, val} <- %{"a" => 1, "b" => 2}, into: %{}, do: {key, val * val}
#=> %{"a" => 1, "b" => 4}
```

---

## Functions

Functions can be defined with `def/2` and private functions with `defp/2`. Private functions can only be called within the module it is defined. Functions are also a type. Arity is the number of parameters of a given function: `Enum.map/2`.

The `&` operator can be used to capture functions, passing `&Module.function/arity` or using `&1`, `&2`, etc. as placholders for parameters. More on this [here](https://hexdocs.pm/elixir/Kernel.SpecialForms.html#&/1).

Named functions must be defined inside modules. You can use `\\` to define default values for your arguments. You can also have [guard clauses](#guard-clauses) using `when` to refine the match on functions.

```elixir
# Anonymous function (lambdas)
square = fn (x) -> x*x end
# Using the & operator
square = &(&1*&1)

square.(2) #=> 4

# Named function
defmodule Math do
  # Inline definition
  def square(x), do: x*x

  # with do/end blocks (syntactic sugar)
  def multiply(x, y) do
    x * y
  end

  # use \\ to default arguments
  # the following example is using a guard clause
  def divide(x, y \\ 2) when y != 0 do
    x / y
  end
end
```

### Guard clauses

Reference for all allowed guard clauses can be found [here](https://hexdocs.pm/elixir/guards).

Guard clauses can also be used in [case](#case) expressions.

```elixir
# List of allowed guard clauses

=== !== == != > >= < <= # Comparison operators
and or not              # Strict boolean operators
+ - * /                 # Arithmetic operators
<>                      # Binary concatenation
in                      # Membership operator

# The following "type-check" functions defined in the Kernel module
is_atom/1, is_binary/1, is_bitstring/1, is_boolean/1, is_float/1,
is_function/1, is_function/2, is_integer/1, is_list/1, is_map/1,
is_nil/1, is_number/1, is_pid/1, is_port/1, is_reference/1, is_tuple/1

# The following "safe" functions defined in the Kernel Module
abs/1, binary_part/3, bit_size/1, byte_size/1, div/2, elem/2,
hd/1, length/1, map_size/1, node/0, node/1, rem/2, round/1,
self/0, tl/1, trunc/1, tuple_size/1
```

---

## Modules

Modules provides namespace for functions. Named functions must be defined inside modules.

```elixir
defmodule Timer do
  def set, do: alert(Time.new(1, 28, 00, 0,  0}))
  defp alert(time), do: "Wake me up at #{time}"
end
```

### Module attributes

Module attributes are variables defined inside modules (not functions) used to annotate the module and as constants. Attributes can be read inside functions. The value exists only during compilation time. `@vsn 2` is an example of module attribute.

| Reserved attributes | Description                            |
| :------------------ | :------------------------------------  |
| `@moduledoc` | provides documentation for the current module |
| `@doc` | provides documentation for the function or macro that follows the attribute |
| `@derive` | use a protocol in the module |
| `@before_compile` | provides a hook that will be invoked before the module is compiled |
| `@enforce_keys` | keys that are mandatory when creating a struct |
| `@type` | used to define new types |
| `@typep` | used to define new private types |
| `@opaque` | used to define a public type where the internal structure is not visible |
| `@spec` | used to define functions |
| `@behaviour` | used for specifying an OTP or user-defined behaviour |
| `@callback` | used to define function callbacks of behaviours |
| `@macrocallback` | used to define macro callbacks of behaviours |

Reference for Typespecs can be found [here](https://hexdocs.pm/elixir/typespecs.html).

### alias

Module names can be aliased.

```elixir
alias Module.Name # Defaults to Name
alias Module.Name, as: OtherName
```

### import

Summons functions and macros. `import` allows easy access to functions and macros from other modules without using the qualified name. By default functions starting with underscore are not imported and must be explicitly included with the `:only` selector.

```elixir
import ModuleName, only: [this: 1]
import ModuleName, except: [this: 1]
import ModuleName, only: :functions
import ModuleName, only: :macros
```

### require

Summons macros. Makes sure the module is compiled and loaded.

### use

Adds capabilities to a module. It invokes the macro `__using__/1` from the module and the returned quoted code is inserted where `use/2` is called.

```elixir
use Module, some: :options
# is the same as
require Module
Module.__using__([some: :options])
```

---

## Protocols

Protocols are a way to achieve polymorphism in Elixir. You have to first define the protocol specifying the functions that are to be implemented:

```elixir
defprotocol Blank do
  def blank?(data)
end
```

And then implement them for each data type (and [struct](#structs)):

```elixir
defimpl Blank, for: List do
  def blank?([]), do: true
  def blank?(_), do: false
end
```

The available types are:

```
Atom, BitString, Float, Function, Integer,
List, Map, PID, Port, Reference, Tuple
```

`Any` can be used to match all types. Setting `@fallback_to_any true` will explicitly tell the protocol to fallback to `Any` when an implementation for that type is not implemented. By setting `@derive ProtocolName` on a struct, the `Any` implementation of that protocol will be available to that struct.

The built-in protocols are:

* [Collectable](https://hexdocs.pm/elixir/Collectable.html)
* [Enumerable](https://hexdocs.pm/elixir/Enumerable.html)
* [Inspect](https://hexdocs.pm/elixir/Inspect.html)
* [List.Chars](https://hexdocs.pm/elixir/List.Chars.html)
* [String.Chars](https://hexdocs.pm/elixir/String.Chars.html)

---

## Behaviours

Behaviours define a set of functions that must be implemented by a module and ensure that a module implements all functions in that set. It separates the generic part of a component (the *behaviour* module) from the specific part (the *callback* module).

```elixir
defmodule MyBehaviour do
  @callback vital_fun() :: any
  @callback non_vital_fun() :: any
  @macrocallback non_vital_macro(arg :: any) :: Macro.t
  @optional_callbacks non_vital_fun: 0, non_vital_macro: 1
end
```

```elixir
defmodule MyCallbackModule do
  @behaviour MyBehaviour
  def vital_fun(arg), do: arg
end
```

The built-in behaviours are:

* [Access](https://hexdocs.pm/elixir/Access.html)
* [Application](https://hexdocs.pm/elixir/Application.html)
* [Calendar](https://hexdocs.pm/elixir/Calendar.html)
* [Exception](https://hexdocs.pm/elixir/Exception.html)
* [GenServer](https://hexdocs.pm/elixir/GenServer.html)
* [Supervisor](https://hexdocs.pm/elixir/Supervisor.html)

---

## Errors

Used only for things that are exceptional. Some keywords for handling errors:

* `raise`
* `try`
* `rescue`
* `throw`
* `catch`
* `after`

```elixir
raise "problem"
#=> ** (RuntimeError) problem

defmodule MyError do
  defexception message: "default message"
end

raise MyError
#=> ** (MyError) default message
raise MyError, message: "custom message"
#=> ** (MyError) custom message

try do
  raise "problem"
rescue
  RuntimeError -> "Error!"
end
#=> "Error!"
```
